//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 7.12 - Настройка
// Объединение групп налогов
//------------------------------------------------------------------------------
#include marker.vih // описание объекта и объявление интерфейса
                    // marker c реализацией по умолчанию

#include exectime.dic  // таблица  для хранения результатов по времени объединения
#include filial_macro.vpp// макросы для обработки филиальности
#include MacOb.vpp //макросы (для таблиц, не содержащих кау, и общие)

#doc
Объединение групп налогов
#end
Interface GrNalOb 'Объединение групп налогов'
  ('Объединение групп налогов', hcNastrAdmWOb, sci1Esc) EscClose, cyan;
  show at (,,80,23);

const
  lMsgOdebdifferenceUser = 'Этим объединением занимается другой пользователь.';
  lFilterByUserOb  = 'FilterByUserObGrNal';
  lNeedZarRunOb    = 'NeedZarRunObGrNal';
  lCheckTimeObedRepOb = 'CheckTimeObedRepObGrNal';
  lNotDefine = 'Удаленная группа налогов';
end;

Create View
#INCLUDE obed.var //объявление переменных
  CountRecord   : integer;
  NameFileMy    : string;
  IError        : integer;
  i             : integer;
  CountRecOrdInProcess :integer; //количество записей для визуализации vtDownVisual
  m_sTitleRep : string;
as Select
  #Strorka_sObName(GrNal,lNotDefine,Name)
  #Strorka_sOb1Name(GrNal1,lNotDefine,Name)
//----------------------------------------------------------------------------------
  #InternalFields
//----------------------------------------------------------------------------------
  // strOfficeJoiHead
  , string(NRec2ONo(GrNal.NRec)) (fieldname = strOfficeJoiHead)
  // strOfficeSpJoi
  , string(NRec2ONo(GrNal1.NRec)) (fieldname = strOfficeSpJoi)
  , if (JoiHead.rComp = comp(1), '+', '') (fieldname = strJoiSpNalog)
  #InternalFieldsForShowFilial
From
  GrNal,
  ExecTime,
  JoiHead,
  SpJoi,
  EvJoiHead
  #TableForFilial,
  GrNal GrNal1,
  GrNal GrNalTmp,
  JoiHead   tmpJoiHead,
  SpJoi     tmpSpJoi,
  EvJoiHead EvJoiHead1
  ,MaskMc, KATMC, SPDOCS, SPDOCNAL, SPSTEP, SPSCHF, KatUsl, SPGRNAL, RASPL,
  MaskUsl
, TUNEVAL, TUNEDEF
, SpSopr
Where
((
  m_wCgObedValue == JoiHead.TypeEvent And    // Группы Налогов
  JoiHead.cRec     == GrNal.nRec      And    // Объединение - Группы Налогов
  JoiHead.nRec     == SpJoi.cJoiHead     And    // Объединение - спецификация
  SpJoi.cRec       == GrNal1.nRec            // Спецификация - Группы Налогов
  #WhereForFilial
))
Bounds OnlyUser =
      m_wCgObedValue == JoiHead.TypeEvent And UserName() == JoiHead.UserName;
File MemoFile;
File FileProtocol;

Const
  iCountTableStep  = 12;
end;

var
  m_pMarkerUp   : IMarker(marker) new;

form ErrObAll ('ErrObGrNal.OUT','ErrObAll') with NoVisual;
form frmObedAll('obedGrNal.out','obedAll') with novisual;
form frmExchngTime('ExchngTime.out', 'ExchngTime') with novisual;
#include obedtime.vpp //процедуры и фукнции по работе с таблицей информации по времени объединения
#include ErrObAll.vpp    // Пакет ошибок
#include printrelat.vpp // отчет замен
#include filial_func.vpp // проверка филиальности
//процедуры и функции обрабатывающие приход реплики с объединеннями  из другого офиса
#include ObFCorpo.vpp
function GetPrintName(cRec : comp) : string;
var
  sResult : string;
{
  sResult := 'Удален';
  If (GetFirst GrNalTmp where ((cRec == GrNalTmp.Nrec)) = tsOk)
    sResult := GrNalTmp.Name;
  result := sResult;
}

Function CheckObj : boolean;
{
  #Check_Obj(GrNalTmp)
}

Function CheckAllRelation : boolean;
{
  NameFileMy := CreateTmpFileName;
  IError := 0;
  if (not FileProtocol.OpenFile(NameFileMy,  stCreate)) {Exit}
  result := True;
  _Loop JoiHead
  {
    _Loop SpJoi
    {
      if (GrNal.Tip <> GrNal1.Tip)
      {
        FileProtocol.Writeln('Группы налогов '+GrNal.Name+' и '+GrNal1.Name+' предназначены для разных объектов ((Товаров, Услуг или Общая)).');
        IError := IError + 1;
      }
    }
  }
  FileProtocol.Close;
  if (IError <> 0)
  {
    if (Message('Возможно некорректное объединение Групп налогов. Смотрите протокол.' + chr(13)
              + 'Будете делать объединение?',Information + YesNo) = Yes) result := True
    else result := False;
  }
}

Procedure ObBreak;  // Прерывание объединения
var
  lcount_out : longint;
{
  lCount_out := VisualCount ();
  Message('Для прекращения объединения придется' + chr(13)
        + 'подождать до окончания замены на' + chr(13)
        + '"' +trim (GrNalTmp.Name)+'".', InfOrmation + OkButton);

  ReStartVisual('Идет замещение групп налогов на'#13''+'"'+ trim(GrNalTmp.Name)+'"' +
                ''#13'',m_lCount_vis);
  NNextVisual (lcount_out);
  m_bBreak := True;
}

Function FindExistObj(cNrecGrNal : comp) : boolean;  // Проверка не участвует ли уже
                                                     // группа налогов в объединении
var
  sObjUp, sObjDown : string[80];
{
  #FindObj(m_wCgObedValue, GrNalTmp, Name, lNotDefine, 'Группа налогов ', cNrecGrNal, 2)
}

procedure CheckProverka;
{
  NameFileMy := CreateTmpFileName;
  IError := 0;
  if (not FileProtocol.OpenFile(NameFileMy,  stCreate)) {Exit}
  _Loop JoiHead
  {
    #CountRecordInTable(MASKMC,CGRNAL,'MASKMC',GrNal,Name)
    #CicleCountRecordInTable(MASKMC,CGRNAL,'MASKMC',GrNal1,Name)
    #CountRecordInTable(MaskUsl,CGRNAL,'MaskUsl',GrNal,Name)
    #CicleCountRecordInTable(MaskUsl,CGRNAL,'MaskUsl',GrNal1,Name)
    #CountRecordInTable(KATMC,CGRNAL,'KATMC',GrNal,Name)
    #CicleCountRecordInTable(KATMC,CGRNAL,'KATMC',GrNal1,Name)
    #CountRecordInTable(SPDOCS,CGRNAL,'SPDOCS',GrNal,Name)
    #CicleCountRecordInTable(SPDOCS,CGRNAL,'SPDOCS',GrNal1,Name)
    #CountRecordInTable(SPDOCNAL,CGRNAL,'SPDOCNAL',GrNal,Name)
    #CicleCountRecordInTable(SPDOCNAL,CGRNAL,'SPDOCNAL',GrNal1,Name)
    #CountRecordInTable(SPSTEP,CGRNAL,'SPSTEP',GrNal,Name)
    #CicleCountRecordInTable(SPSTEP,CGRNAL,'SPSTEP',GrNal1,Name)
    #CountRecordInTable(SPSCHF,CGRNAL,'SPSCHF',GrNal,Name)
    #CicleCountRecordInTable(SPSCHF,CGRNAL,'SPSCHF',GrNal1,Name)
    #CountRecordInTable(KATUSL,CGRNAL,'KATUSL',GrNal,Name)
    #CicleCountRecordInTable(KATUSL,CGRNAL,'KATUSL',GrNal1,Name)
    if (JoiHead.rComp = comp(1))//только при включенной настройке
    {
      #CountRecordInTable(SPGRNAL,CGRNAL,'SPGRNAL',GrNal,Name)
      #CicleCountRecordInTable(SPGRNAL,CGRNAL,'SPGRNAL',GrNal1,Name)
    }
    #CountRecordInTable(RASPL,CGRNALFORAVANCE,'RASPL',GrNal,Name)
    #CicleCountRecordInTable(RASPL,CGRNALFORAVANCE,'RASPL',GrNal1,Name)
    // меняю настроку "Группа налогов по умолчанию для МЦ"
    #CountRecordInTableTune('McUsl.mcDefGrNal')
    #CicleCountRecordInTableTune('McUsl.mcDefGrNal')
  }

  FileProtocol.Close;
  if (IError <> 0)
  {
    ProcessText(NameFileMy,vfToErase Or vfEscable Or vfMacroSize,
                'Проверка в объединениях Групп налогов');
  }
}

Function updateObj : boolean;
{
  m_bBreak    := False;
  updateObj:= False;
  m_bShowRepTime := true;
  _nMsgFrm  := 0;          // Общее число ошибок
  m_lErrOrJoi  := 0;
  m_lErrOrSp   := 0;
  m_lCount_vis := 0;

  _loop JoiHead where ((    m_wCgObedValue == JoiHead.TypeEvent
                        and UserName() == JoiHead.UserName ))
  {
    case JoiHead.Status of
    word(0) : if (m_wParamOb = word(1)) continue;
    word(1) : {};
    word(3) :  continue;
    word(4) : if (m_wParamOb <> word(0)) continue;
    end;

    if (JoiHead.prCOrpo <> byte(0))
      if (CheckObj)
      {
        message ('Одно из объединений, присланное по COrpo обмену'
                 + chr(13) + 'будет отложено, поскольку не существует группы налогов,'
                 + chr(13) + 'в которую необходимо производить объединение.');
        continue;
      }

    _loop SpJoi where ((    JoiHead.nRec == SpJoi.cJoiHead
                        and (word(3) <> SpJoi.Status) ))
      m_lCount_vis := m_lCount_vis + iCountTableStep;
  }

  if (m_lCount_vis <> 0)
  {
    if (message('Если Вы не сохранили данные, еще раз рекомендуем это сделать.' + chr(13)
              + 'Запустить объединение групп налогов?', YesNo + mfSwapButtons + Confirmation) = cmNo)
    {
      m_bShowRepTime := false;
      exit;
    }
    StartNewVisual(vtIndicatOrVisual, vfTimer + vfBreak + vfConfirm,''#13#13#13'',m_lCount_vis);
  }
  else
  {
    message ('Нет заданий на объединение.');
    m_bShowRepTime := false;
    m_wParamOb := 0;
    exit;
  }
  MyMarker:=InitMarker('', 8, 50, 50, false);

                // Цикл по результирующей
  if (getFirst JoiHead where ((    m_wCgObedValue == JoiHead.TypeEvent
                               and UserName() == JoiHead.UserName )) = tsOk )
  do
  {
    case JoiHead.Status of
    word(0) : if (m_wParamOb = word(1)) continue;
    word(1) : {};
    word(3) :  continue;
    word(4) : if (m_wParamOb <> word(0)) continue;
    end;

    if (JoiHead.prCOrpo <> byte(0))
      if (CheckObj) Continue;

    if (    (m_bCOrpo)
        and (JoiHead.prCOrpo = byte(0))
        and (JoiHead.Status = word(0)))
    {
      ClearBuffer (#EvJoiHead);
      EvJoiHead.FilialNo := OfficeNo();
      EvJoiHead.TypeEvent := m_wCgObedValue;
      EvJoiHead.cRec := JoiHead.cRec;
      EvJoiHead.dDate := Cur_date();
      MemoFile.OpenMemo (EvJoiHead.EvSp);
      MemoFile.Write(JoiHead.rComp);
      _loop SpJoi where ((JoiHead.nRec == SpJoi.cJoiHead))
      {
        MemoFile.Write(SpJoi.cRec);
        MemoFile.Write(SpJoi.Num);
        MemoFile.Write(SpJoi.isDel);
/*
        If (GetFirst GrNalTmp where ((SpJoi.cRec == GrNalTmp.nRec)) = tsOk)
          MemoFile.Write(GrNalTmp.Name);
        Else
          MemoFile.Write(lNotDefine);
*/
      }
      MemoFile.Close;
      insert current EvJoiHead;
    }

    JoiHead.Status := wOrd(1);  // Делаем статус "Начата"
    Update Current JoiHead;

    If ( GetFirst GrNalTmp where (( JoiHead.cRec == GrNalTmp.nrec)) = tsOk ) {};

    iNextVisual('Идет замещение групп налогов на'#13''+'"'+ trim(GrNalTmp.Name)+'"' +
                ''#13'');
    m_bWriteInfoSpJoi := True;
                //  Цикл по объединяемым
    if (getFirst SpJoi where ((    JoiHead.nRec == SpJoi.cJoiHead
                               and (word(3) <> SpJoi.Status) )) = tsOk)
    do
    {
      SpJoi.Status := wOrd(1);
      Update current SpJoi;
//table 1
#UpdateTableOneFieldNoIndex(MASKMC,CGRNAL,'MASKMC')
//table 3
#UpdateTableOneFieldNoIndex(KATMC,CGRNAL,'KATMC')
//table 4
#UpdateTableOneFieldNoIndex(SPDOCS,CGRNAL,'SPDOCS')
//table 5
#UpdateTableOneFieldNoIndex(SPDOCNAL,CGRNAL,'SPDOCNAL')
//table 6
#UpdateTableOneFieldNoIndex(SPSTEP,CGRNAL,'SPSTEP')
//table 7
#UpdateTableOneFieldNoIndex(SPSCHF,CGRNAL,'SPSCHF')
//table 8
#UpdateTableOneFieldNoIndex(RASPL,CGRNALFORAVANCE,'RASPL')
//table 9
#UpdateTableOneFieldNoIndex(KATUSL,CGRNAL,'KATUSL')
//table 10
if (JoiHead.rComp = comp(1))//только при включенной настройке
{
  #UpdateTableOneFieldWithIndex(SPGRNAL,CGRNAL,'SPGRNAL')
}
//table12
#UpdateTableOneFieldNoIndex(MaskUsl,CGRNAL,'MaskUsl')
//SPSOPR
#UpdateTableOneFieldNoIndex(SPSOPR,CGRNAL,'SPSOPR')

// меняю настроку "Группа налогов по умолчанию для МЦ"
#UpdateTableTune('McUsl.mcDefGrNal')
        // Если этап завершен без ошибок для одной рез. МЦ
        If (_nMsgFrm = m_lErrOrSp) SpJoi.Status := wOrd(3);
        else                       SpJoi.Status := wOrd(4);
        Update current SpJoi;

        If (m_lErrOrSp <> _nMsgFrm)
        {
          ErrObAll.PutEventById (feBreak, fcCycleOfErrOrAll);
          ErrObAll.write(_nMsgFrm - m_lErrOrSp);
        }

        m_lErrOrSp := _nMsgFrm;
    } while (GetNext SpJoi where ((    JoiHead.nRec == SpJoi.cJoiHead
                              and (word(3)<>SpJoi.Status) )) = tsOk);

    m_bWriteInfoSpJoi := False;

    If (m_lErrOrJoi <> _nMsgFrm)
    {
      ErrObAll.PutEventById (feBreak, fcCycleOfObAll);
      ErrObAll.write(_nMsgFrm - m_lErrOrJoi);
    }
    // Если объединение без ошибок
    If (_nMsgFrm = m_lErrOrJoi) JoiHead.Status := wOrd(3); // статус "Завершено"
    else                        JoiHead.Status := wOrd(4);
    Update Current JoiHead;      // статус "Завершено с ошибками"

    m_lErrOrJoi := _nMsgFrm;
    m_lErrOrSp := m_lErrOrJoi;
  } while (    (GetNext JoiHead where ((    m_wCgObedValue == JoiHead.TypeEvent
                                        and UserName() == JoiHead.UserName )) = tsOk)
           and (m_bBreak = false));

  if (_nMsgFrm > 0)
  {
    ErrObAll.PutEventById (feBreak, fcCycleOfResAll);
    ErrObAll.write(_nMsgFrm);
  }

  m_wParamOb := 0;
  DoneMarker (MyMarker,'');
  If (m_lCount_vis <> 0)
  {
    StopVisual('', 0);
    If (_nMsgFrm > 0)  // Если были ошибки во время объединения
    {
      message('Во время объединения произошли ошибки.'
              + chr(13) + 'Просмотрите протокол ошибок и если необходимо,'
              + chr(13) + 'запустите процедуру объединения групп налогов еще раз.' + chr(13));
      ErrObAll.ShowFile;
    }
    else
      Message('Объединение завершено.');
  }
}

browse GrNalJoi;
show at (,,,6);
Table JoiHead;
  recMarker = m_pMarkerUp {JoiHead.Nrec};
fields
  strOfficeJoiHead 'Офис', '───' ('',hcNastrAdmWOb,sci1378EscInsPMBrigadeDuty): [5], protect;
  sObName          'Наим. групп налогов. (рез.объединения)'
                   ('', hcNastrAdmWOb, sci1378EscInsPMBrigadeDuty) : [32], Protect;
  strFilial        'филиал','───────'('',hcNastrAdmWOb,sci1Esc) : [7], Protect;
  strJoiStatus     'Статус'
                   ('', hcNastrAdmWOb, sci1Esc) : [14], Protect;
  strJoiSpNalog   'Переместить спецификации', ('Изменяется при нажатии на <F3>',hcNastrAdmWOb,sci13Esc): [9], protect;
  JoiHead.UserName 'Пользователь'('', hcNastrAdmWOb, sci13Esc) : [12], Protect;
  strJoiCOrpo      '№ офиса','───────'('',hcNastrAdmWOb,sci1Esc) : [7], Protect;
end;

TableEvent Table JoiHead;
cmSetDefault :
{
  JoiHead.TypeEvent := m_wCgObedValue;
  JoiHead.Status := word(0);
  JoiHead.UserName := UserName();
  JoiHead.dDate := Cur_Date;
}

cmInsertRecord : insert current JoiHead;
cmUpdateRecord : update current JoiHead;

cmInsert :
{
  case curfield of
  #sObName :
  {
    if (RunInterface('GetGrNal', m_cChoiceNrec, 0)<>cmCancel)
    {
      if (not FindExistObj(m_cChoiceNrec)) putCommand(cmValue1);
      else abort;
    }
    else abort;
  }
  end;
}

cmMarkDelete :
{
  if (curtable = #JoiHead)
  {
    m_bDeleteByMarker := false;
    if (m_pMarkerUp.Count <> 0)
    {
      if (message('Удалить помеченные записи?', yesno + mfSwapButtons + confirmation) = cmYes)
      {
        m_bDeleteByMarker := true;
        stop;
      }
      else abort;
    }
  }
}

cmDeleteRecOrd :
{
  if not(isValid(tnJoiHead)) exit;
  if (not m_bDeleteByMarker) m_bExistDel := false; // инициализация переменной в случае когда пометки нет для случая обычного удаления

  //#5578 >>>>>>>>>>>>>>>>>
  var _boDiffUser : boolean = (JoiHead.UserName <> UserName());
  if _boDiffUser
    if (not m_bDeleteByMarker)
      if pr_CurUserAdmin
      { if message(lMsgOdebdifferenceUser
          +chr(13)+'Продолжить?' , YesNo)=cmYes
        { _boDiffUser:=false

        }
      }
  //#5578 <<<<<<<<<<<<<<<<<<<<<<<<<
  if _boDiffUser
  {
    if (not m_bDeleteByMarker)
      message(lMsgOdebdifferenceUser
        +chr(13)+'обратитесь к администратору БД', Warning);
    if (m_bExistDel and ((not m_bDeleteByMarker) or (m_bDeleteByMarker and m_pMarkerUp.Count = 0)))
      message('Удаление из каталога МЦ произведено.', Information + OkButton);
    exit;
  }

  var bRun : boolean; bRun := true;
  if (not m_bDeleteByMarker)
    bRun := message('Удалить запись?', yesno + mfSwapButtons + confirmation) = cmYes;

  if (bRun)
  {
    if (    (JoiHead.Status = word(3))
         Or (JoiHead.Status = word(4)))
    {
      if (getFirst SpJoi where ((    JoiHead.nRec == SpJoi.cJoiHead
                                 and byte(1)      == SpJoi.isDel
                                 and byte(4)      == SpJoi.Status )) = tsOk)
      {
        var bDelFromCatalog : boolean; bDelFromCatalog := false;
        if (not m_bDeleteByMarker)
          m_bDeleteByMarker := message('Удалять из каталога групп налогов, объединение'
                    + chr(13) + 'которых завершено с ошибками?',
                    yesno + mfSwapButtons + Confirmation) = cmYes;
        if (m_bDeleteByMarker)
          update SpJoi where ((    JoiHead.nRec == SpJoi.cJoiHead
                               and byte(1)      == SpJoi.isDel
                               and byte(4)      == SpJoi.Status)) set Status := 3;
      }

      _loop SpJoi where ((    JoiHead.nRec == SpJoi.cJoiHead
                          and byte(1)      == SpJoi.isDel
                          and byte(3)      == SpJoi.Status))
      {
        // при удалении групп налогов проверяем, если не нужно переносить спецификации
        // групп налогов то, чтобы они не висели в бд мертвым грузом ибо самой группы
        // не будет - удаляем эти спецификации
        if (JoiHead.rComp <> comp(1))
          delete SpGrNal where ((SpJoi.cRec == SpGrNal.cGrNal));
        delete GrNalTmp where ((SpJoi.cRec == GrNalTmp.nrec));
        m_bExistDel := true;
      }
    }
    delete SpJoi where ((JoiHead.nRec == SpJoi.cJoiHead));
    delete current JoiHead;
    if (m_bExistDel and ((not m_bDeleteByMarker) or (m_bDeleteByMarker and m_pMarkerUp.Count = 0)))
      Message('Удаление из каталога групп налогов произведено.', InfOrmation + OkButton);
  } else Abort;
}
end;

hAndleevent
cmValue1 :
{
  set JoiHead.rComp := comp(1);
  set JoiHead.cRec := m_cChoiceNrec;
  UpdateTable;
  SetFilialInNastrObed(coJoiHead, GrNal.Name, GrNal.Atl_Branch);
  ReDrawPanel(#JoiHead);
}

cmPick :
{
  #Check_Pick(JoiHead)
  case curfield of
  //-----------
  #sObName :
  {
    if (JoiHead.Status <> 0) exit;
    m_cChoiceNrec := JoiHead.cRec;
    if (RunInterface('GetGrNal', m_cChoiceNrec, 0) <> cmCancel)
    {
      #CheckChoiceValueForJoiHeadOnPick
      set JoiHead.rComp := comp(1);
      UpDateTable;
      SetFilialInNastrObed(coJoiHead, GrNal.Name, GrNal.Atl_Branch);
      ReReadRecOrd(#JoiHead);
      ReDrawPanel(#JoiHead);
    }
  }
  #strJoiSpNalog :
  {
    if (JoiHead.nRec <> comp(0))
    {
      if (JoiHead.rComp = comp(1)) set JoiHead.rComp := comp(0);
      else                         set JoiHead.rComp := comp(1);
    }
  }
  end;
}
end; // hanldeevent

browse GrNalSpJoi;
show at (,7,,17);
Table SpJoi;
fields
  strOfficeSpJoi 'Офис', '───' ('',hcNastrAdmWOb,sci1378Esc): [5], protect;
  sOb1Name        'Наим. групп налогов, подлежащих объединению'
                  ('', hcNastrAdmWOb, sci1378Esc): [41], Protect;
  strFilial1      'филиал','───────'('',hcNastrAdmWOb, sci1Esc) : [7], Protect;
  strSpJoiStatus  'Статус'
                  ('', hcNastrAdmWOb, sci1Esc)   : [15], Protect;
  cSpJoiDel       'Удалять'
                  ('Изменяется при нажатии на <F3>', hcNastrAdmWOb, sci13Esc)
                                                          : [7], #protectedcentered;
end;

tableEvent Table SpJoi;
cmInsert:
{
  if (JoiHead.UserName <> UserName())
  {
    message (lMsgOdebdifferenceUser, Warning);
    abort; exit;
  }

  if (JoiHead.Status <> 0) {abort; exit;}

  if (RunInterface('GetGrNal', m_cChoiceNrec, 0) <> cmCancel)
  {
    if (FindExistObj(m_cChoiceNrec))
    {
      abort;
      exit;
    }
    ClearBuffer (#SpJoi);
    SpJoi.cJoiHead := JoiHead.nRec;
    SpJoi.cRec := m_cChoiceNrec;
    SpJoi.Status := word(0);
    SpJoi.IsDel := word(1);
    Insert current SpJoi;
    SetFilialInNastrObed(coSpJoi, GrNal1.Name, GrNal1.Atl_Branch);
    ReReadRecord(#SpJoi);
    ReDrawPanel(#SpJoi);
  }
  abort;
}

cmdeleteRecord :
{
  if (JoiHead.UserName <> UserName())
  {
    message (lMsgOdebdifferenceUser, Warning);
    exit;
  }

  if (JoiHead.Status = 0)
    if (message('Удалить запись?', yesno + mfSwapButtons + Confirmation) = cmYes)
      delete current SpJoi;
}
end;

hAndleevent
cmPick :
{
  #Check_Pick(SpJoi)

  case CurField of
  #sOb1Name :
  {
    if (JoiHead.Status <> 0) exit;
    m_cChoiceNrec := SpJoi.cRec;
    if (RunInterface('GetGrNal', m_cChoiceNrec, 0) <> cmCancel)
    {
      #CheckChoiceValueForSpJoiOnPick
      ClearBuffer (#SpJoi);
      SpJoi.cJoiHead := JoiHead.nRec;
      SpJoi.cRec := m_cChoiceNrec;
      SpJoi.Status := word(0);
      SpJoi.IsDel := word(1);
      Insert current SpJoi;
      SetFilialInNastrObed(coSpJoi, GrNal1.Name, GrNal1.Atl_Branch);
      ReReadRecord(#SpJoi);
      ReDrawPanel(#SpJoi);
    }
  }
  #cSpJoiDel :
  {
    if (SpJoi.cRec <> comp(0))
    {
      if (SpJoi.IsDel = word(1)) SpJoi.IsDel := word(0);
      else                       SpJoi.IsDel := word(1);
      update current SpJoi;
      RedrawPanel (#SpJoi);
    }
  }
  end;
}

end; // hAndleevent

// Экран с кнопками
screen ButtonsList;
  show at (,18,,22);
  Fields
    m_wFilterByUser ('Изменяется при нажатии на <Пробел>', hcNastrAdmWOb, sci1Esc) : , noprotect;
    m_wCheckTimeObedRep ('Изменяется при нажатии на <Пробел>',hcNastrAdmWOb,sci1Esc): noprotect;
Buttons
  cmObed,Default,,,hcNastrAdmWOb,sci1Esc;
  cmCancel,,,,hcNastrAdmWOb,sci1Esc;
<<
  Показывать объединения
  (.) всех пользователей`      [.] Протокол по времени объединения`
  (.) только свои       `
                             <.~О~бъединить .>         <. О~т~мена .>
>>

end; // screen

hAndleevent
cmCheckField :
{
  case curfield of
  #m_wFilterByUser :
  {
    case m_wFilterByUser of
    0 :  if (BoundActive(tbOnlyUser))     PopBounds (tbOnlyUser);
    1 :  if (not BoundActive(tbOnlyUser)) PushBounds (tbOnlyUser);
    end;
    if (getFirst JoiHead = tsOk) {};

    ReReadRecord(#JoiHead);
    RescanPanel (#JoiHead);
    RescanPanel (#SpJoi);
    SelectPanel (#JoiHead);
    SaveMyDsk (m_wFilterByUser, lFilterByUserOb);
  }
 // #m_wNeedZarRun : SaveMyDsk (m_wNeedZarRun, lNeedZarRunOb);
  #m_wCheckTimeObedRep : SaveMyDsk (m_wCheckTimeObedRep, lCheckTimeObedRepOb);
  end; // case
}

cmInit:
{
  DisableCommand(cmFilterSave);
  m_wCodeTable := coGrNal;
 // функцию можно запускать при выборе режима одного филиала
  if (not CanMayRunObedInFilialDb)
  {
    CloseInterface(cmCancel);
    exit;
  }
  bDBPlatformType := GetDBPlatformType;// читаем тип СУБД
  m_wCgObedValue := cgObedGrNal;// присвоение должно быть раньше кода, где используется переменная m_wCgObedValue
  if (not ReadMyDsk (m_wNeedZarRun, lNeedZarRunOb, true)) m_wNeedZarRun := 3;
  if (not ReadMyDsk (m_wFilterByUser, lFilterByUserOb, true)) m_wFilterByUser := 0;
  if (not ReadMyDsk (m_wCheckTimeObedRep, lCheckTimeObedRepOb, true)) m_wCheckTimeObedRep := 0;
  if (m_wFilterByUser = 1) PushBounds (tbOnlyUser);

  m_wParamOb := 0;
  m_bCOrpo := InCorpo(m_wCgObedValue);

  if (m_bCOrpo)
  {
    DelOutCOrpo(m_wCgObedValue);
    CorpoForUser(m_wCgObedValue);
  }

  if (getFirst tmpJoiHead where ((    m_wCgObedValue == tmpJoiHead.TypeEvent
                                  and UserName()        == tmpJoiHead.UserName
                                  and (word(1)=tmpJoiHead.Status) )) = tsOk )
  {
    m_wParamOb := word(1);
    if (message('Обнаружено незавершенное объединение групп налогов'
                + chr(13) + 'рекомендуется немедленно продолжить это объединение.'
                + chr(13) + chr(13) + 'Запустить незавершенное объединение?',yesno + Confirmation) = cmYes)
      PutCommand(cmObed);
    else
      CloseInterface (cmCancel);
  }
}

cmDefault:
  AbOrt;

cmHotKeys :
if #IsBetaTester
  PutHotCommAnd(RunMenu('BetaObedGrNal_HotKeys_Print'));
else
  PutHotCommAnd(RunMenu('ObedGrNal_HotKeys_Print'));

cmPrintDoc :
{
  if (frmObedAll.errOr)
  {
    frmObedAll.abortform;
    message('Ошибка при формировании протокола соответствия.' + chr(13)
           +'Возможно, формы ObedAll нет в ресурсе.', CancelButton);
    exit;
  }
  else
    #PrintRelation(GrNal1,GrNal,Name,'групп налогов')
}

cmObed :
{
  _try
  {
    SetFilialBdToStandart;// обработка филиальности (перевод с филиальности в стандарт)
    mtClear(#ExecTime, mfNormal);
    if (m_wParamOb <> 1)
    {
      _loop tmpJoiHead where ((    m_wCgObedValue == tmpJoiHead.TypeEvent
                               and UserName()        == tmpJoiHead.UserName))
      {
        if (getFirst tmpSpJoi where ((tmpJoiHead.nRec == tmpSpJoi.cJoiHead)) <> tsOk)
        {
          var sObjName : string; sObjName := lNotDefine;
          if (GetFirst GrNalTmp where (( tmpJoiHead.cRec == GrNalTmp.nrec)) = tsOk )
            sObjName := trim(GrNalTmp.Name);
          message ('Для результата объединения' + chr(13)
                 + '"' + sObjName + '"' + chr(13)
                 + 'не выбраны группы налогов, которые необходимо объединять.', CancelButton);
          SetStandartBdToFilial;// обработка филиальности (перевод со стандартной в филиальную)
          exit;
        }
      }

      if (getFirst tmpJoiHead where ((    m_wCgObedValue == tmpJoiHead.TypeEvent
                                      and UserName()        == tmpJoiHead.UserName
                                      and (word(4) = tmpJoiHead.Status) )) = tsOk )
        if (message('Хотите повторять объединение для' + chr(13)
                  + 'завершенных с ошибками групп налогов?', YesNo + Confirmation) = cmNo)
          m_wParamOb := 2;
    }
    if (IsNotExistNastroikiFromOtherFilials(m_wCgObedValue, m_wCodeTable))
      if (CheckAllRelation)
      {
        updateObj;
      }
      else if (IError <> 0)
      {
        ProcessText(NameFileMy,vfToErase Or vfEscable Or vfMacroSize,
                   'Отчет о некорректных объединениях в объединении Групп налогов');
      }
  }
  _finally
  {
    SetStandartBdToFilial;// обработка филиальности (перевод со стандартной в филиальную)
  }
  ReReadRecOrd(#JoiHead);
  ReDrawPanel(#JoiHead);
  ReReadRecOrd(#SpJoi);
  ReDrawPanel(#SpJoi);
  if ((m_wCheckTimeObedRep = 1) and m_bShowRepTime)
    #MakeTimeProt('групп налогов');
}

cmValue2 :
{
  CheckProverka;
}
end;
end.  // Interface

ObedGrNal_HotKeys_Print Menu
{
- 'Печать',cmPrintDoc,'Печать соответствия объединения',, 'Ctrl+P', kbCtrlP, scMenuCtx;
}

BetaObedGrNal_HotKeys_Print Menu
{
- 'Печать',cmPrintDoc,'Печать соответствия объединения',, 'Ctrl+P', kbCtrlP, scMenuCtx;
- 'Проверка объединения ',cmValue2,'Проверка объединения',,'',, scMenuCtx;
}
