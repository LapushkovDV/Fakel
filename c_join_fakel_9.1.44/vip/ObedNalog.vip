//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 7.12 - Настройка
// Объединение налогов
//------------------------------------------------------------------------------
#include marker.vih // описание объекта и объявление интерфейса
                    // marker c реализацией по умолчанию

#include exectime.dic  // таблица  для хранения результатов по времени объединения
#include filial_macro.vpp// макросы для обработки филиальности
#include MacOb.vpp //макросы (для таблиц, не содержащих кау, и общие)

#doc
Объединение налогов
#end
Interface KatNalogOb 'Объединение налогов'
  ('Объединение налогов', hcNastrAdmWOb, sci1Esc) EscClose, cyan;
  show at (,,80,23);

const
  lMsgOdebdifferenceUser = 'Этим объединением занимается другой пользователь.';
  lFilterByUserOb  = 'FilterByUserObNalog';
  lNeedZarRunOb    = 'NeedZarRunObNalog';
  lCheckTimeObedRepOb = 'CheckTimeObedRepObNalog';
  lNotDefine = 'Удаленный налог';
  lPickConst = 21;
end;

Create View
#INCLUDE obed.var //объявление переменных
  CountRecord  : integer;
  NameFileMy   : string;
  IError       : integer;
  CountRecOrdInProcess :integer; //количество записей для визуализации vtDownVisual
  m_sTitleRep : string;
as Select
  #Strorka_sObName(KatNalog,lNotDefine,Name)
  #Strorka_sOb1Name(KatNalog1,lNotDefine,Name)
//----------------------------------------------------------------------------------
  #InternalFields
//----------------------------------------------------------------------------------
  // strOfficeJoiHead
  , string(NRec2ONo(KatNalog.NRec)) (fieldname = strOfficeJoiHead)
  // strOfficeSpJoi
  , string(NRec2ONo(KatNalog1.NRec)) (fieldname = strOfficeSpJoi)
//----------------------------------------------------------------------------------
  #InternalFieldsForShowFilial
From
  KatNalog,
  ExecTime,
  JoiHead,
  SpJoi,
  EvJoiHead
  #TableForFilial,
  KatNalog KatNalog1,
  KatNalog KatNalogTmp,
  JoiHead   tmpJoiHead,
  SpJoi     tmpSpJoi,
  EvJoiHead EvJoiHead1,
  FormPrLs, SPDOCNAL, SPGRNAL, NALOGFIN, Tunedef, Tuneval, Pick
Where
((
  m_wCgObedValue == JoiHead.TypeEvent And    // Налоги
  JoiHead.cRec     == KatNalog.nRec      And    // Объединение - Налоги
  JoiHead.nRec     == SpJoi.cJoiHead     And    // Объединение - спецификация
  SpJoi.cRec       == KatNalog1.nRec            // Спецификация - Налоги
  #WhereForFilial
))
Bounds OnlyUser =
      m_wCgObedValue == JoiHead.TypeEvent And UserName() == JoiHead.UserName;
File MemoFile;
File FileProtocol;

var
  m_pMarkerUp   : IMarker(marker) new;

form ErrObAll ('ErrObNalog.OUT','ErrObAll') with NoVisual;
form frmObedAll('obedNalog.out','obedAll') with novisual;
form frmExchngTime('ExchngTime.out', 'ExchngTime') with novisual;
#include obedtime.vpp //процедуры и фукнции по работе с таблицей информации по времени объединения
#include ErrObAll.vpp    // Пакет ошибок
#include printrelat.vpp // отчет замен
#include filial_func.vpp // проверка филиальности

//процедуры и функции обрабатывающие приход реплики с объединеннями  из другого офиса
#include ObFCorpo.vpp
function GetPrintName(cRec : comp) : string;
var
  sResult : string;
{
  sResult := 'Удален';
  If (GetFirst KatNalogTmp where ((cRec == KatNalogTmp.Nrec)) = tsOk)
    sResult := KatNalogTmp.Name;
  result := sResult;
}

Function CheckObj : boolean;
{
  #Check_Obj(KatNalogTmp)
}

procedure CheckProverka;
{
  NameFileMy := CreateTmpFileName;
  IError := 0;
  if (not FileProtocol.OpenFile(NameFileMy,  stCreate)) {Exit}
  _Loop JoiHead
  {
    #CountRecordInTable(SPDOCNAL,CNALOG,'SPDOCNAL',KatNalog,Name)
    #CicleCountRecordInTable(SPDOCNAL,CNALOG,'SPDOCNAL',KatNalog1,Name)
    #CountRecordInTable(SPGRNAL,CNALOG,'SPGRNAL',KatNalog,Name)
    #CicleCountRecordInTable(SPGRNAL,CNALOG,'SPGRNAL',KatNalog1,Name)
    #CountRecordInTable(NALOGFIN,CNALOG,'NALOGFIN',KatNalog,Name)
    #CicleCountRecordInTable(NALOGFIN,CNALOG,'NALOGFIN',KatNalog1,Name)
    #CountRecordInTableTune('Nalog.cNalogNDS')
    #CicleCountRecordInTableTune('Nalog.cNalogNDS')
    #CountRecordInTableTune('Nalog.cNalogProd')
    #CicleCountRecordInTableTune('Nalog.cNalogProd')
    #CountRecordInTableTune('Nalog.cNalogAkcis')
    #CicleCountRecordInTableTune('Nalog.cNalogAkcis')
    #CountRecordInTableWithTwoField(FormPrLs,cNalNac,TipNN,0,'FormPrLs',KatNalog,Name)
    #CicleCountRecordInTableWithTwoField(FormPrLs,cNalNac,TipNN,0,'FormPrLs',KatNalog1,Name)
  }

//DogStroy.cDogovor - Женя Боровский сказал что в принципепе ненадо менгять в даннойтаблице.чуть что там монжо бует персчитать

  FileProtocol.Close;
  if (IError <> 0)
  {
    ProcessText(NameFileMy,vfToErase Or vfEscable Or vfMacroSize,
                'Проверка в объединениях налогов');
  }
}

Procedure ObBreak;  // Прерывание объединения
var
  lcount_out : longint;
{
  lcount_out := VisualCount();
  message('Для прекращения объединения придется'
          + chr(13) + 'подождать до окончания замены на'
          + chr(13) + '"' + trim (KatNalogTmp.Name) + '".', Information + OkButton);

  ReStartVisual('Идет замещение налога на'#13'' + '"' + trim(KatNalogTmp.Name)
                +'"' + ''#13'', m_lcount_vis);
  NNextVisual (lcount_out);
  m_bBreak := true;
}

Function FindExistObj(cNrecNalog : comp) : boolean;  // Проверка не участвует ли уже
                                                     // налог в объединении
var
  sObjUp, sObjDown : string[80];
{
  #FindObj(m_wCgObedValue, KatNalogTmp, Name, lNotDefine, 'Налог ', cNrecNalog, 0)
}

Function updateObj : boolean;
{
  m_bBreak    := False;
  result := False;
  m_bShowRepTime := true;
  _nMsgFrm  := 0;          // Общее число ошибок
  m_lErrOrJoi  := 0;
  m_lErrOrJoi   := 0;
  m_lCount_vis := 0;

  _loop JoiHead where ((    m_wCgObedValue == JoiHead.TypeEvent
                        and UserName() == JoiHead.UserName ))
  {
    case JoiHead.Status of
    word(0) : if (m_wParamOb = word(1)) continue;
    word(1) : {};
    word(3) :  continue;
    word(4) : if (m_wParamOb <> word(0)) continue;
    end;

    if (JoiHead.prCOrpo <> byte(0))
      if (CheckObj)
      {
        message ('Одно из объединений, присланное по COrpo обмену'
                 + chr(13) + 'будет отложено, поскольку не существует налога,'
                 + chr(13) + 'в которую необходимо производить объединение.');
        continue;
      }

    _loop SpJoi where ((    JoiHead.nRec == SpJoi.cJoiHead
                        and (word(3) <> SpJoi.Status) ))
       m_lCount_vis := m_lCount_vis +4;
  }

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
  if (m_lcount_vis <> 0)
  {
    if (message('Если Вы не сохранили данные, еще раз рекомендуем это сделать.' + chr(13)
              + 'Запустить объединение налогов?', YesNo + mfSwapButtons + Confirmation) = cmNo)
    {
      m_bShowRepTime := false;
      exit;
    }
    StartNewVisual(vtIndicatOrVisual, vfTimer + vfbreak + vfConfirm,''#13#13#13'', m_lcount_vis);
  }
  else
  {
    message ('Нет заданий на объединение.');
    m_bShowRepTime := false;
    m_wParamOb := 0;
    exit;
  }
  MyMarker:=InitMarker('', 8, 50, 50, false);
                // Цикл по результирующей
  If (GetFirst JoiHead Where ((m_wCgObedValue == JoiHead.TypeEvent And
                               UserName()        == JoiHead.UserName )) = tsOk )
  do
  {
    case JoiHead.Status of
    word(0) : if (m_wParamOb = word(1)) continue;
    word(1) : {};
    word(3) :  continue;
    word(4) : if (m_wParamOb <> word(0)) continue;
    end;

    if (JoiHead.prCOrpo <> byte(0))
      if (CheckObj) continue;
                        // Пересылка если COrpo
    if (    (m_bCOrpo)
        and (JoiHead.prCOrpo = byte(0))
        and (JoiHead.Status = word(0)))
    {
      ClearBuffer (#EvJoiHead);
      EvJoiHead.FilialNo := OfficeNo();
      EvJoiHead.TypeEvent := m_wCgObedValue;
      EvJoiHead.cRec := JoiHead.cRec;
      EvJoiHead.dDate := Cur_date();
      MemoFile.OpenMemo (EvJoiHead.EvSp);
      _loop SpJoi where ((JoiHead.nRec == SpJoi.cJoiHead))
      {
        MemoFile.Write(SpJoi.cRec);
        MemoFile.Write(SpJoi.Num);
        MemoFile.Write(SpJoi.isDel);
/*
        If (GetFirst KatNalogTmp where ((SpJoi.cRec == KatNalogTmp.nRec)) = tsOk)
          MemoFile.Write(KatNalogTmp.Name);
        Else
          MemoFile.Write(lNotDefine);
*/
      }
      MemoFile.Close;
      insert current EvJoiHead;
    }

    JoiHead.Status := wOrd(1);  // Делаем статус "Начата"
    Update Current JoiHead;

    If ( GetFirst KatNalogTmp where (( JoiHead.cRec == KatNalogTmp.nrec)) = tsOk ) {};

    iNextVisual('Идет замещение налог на'#13''+'"'+ trim(KatNalogTmp.Name)+'"' +
                ''#13'');
    m_bWriteInfoSpJoi := True;

                //  Цикл по объединяемым
    If (GetFirst SpJoi where ((JoiHead.nRec == SpJoi.cJoiHead And (word(3)<>SpJoi.Status))) = tsOk)
      do
      {
        SpJoi.Status := wOrd(1);
        Update current SpJoi;

// table 1
#UpdateTableOneFieldWithIndex(SPDOCNAL, CNALOG, 'SPDOCNAL')
// table 2
#UpdateTableOneFieldWithIndex(SPGRNAL, CNALOG, 'SPGRNAL')
// table 3
#UpdateTableTune('Nalog.cNalogNDS')
#UpdateTableTune('Nalog.cNalogProd')
#UpdateTableTune('Nalog.cNalogAkcis')
// table 5
#UpdateTableOneFieldNoIndex(NALOGFIN, CNALOG, 'NALOGFIN')
// table 6
#UpdateTableOneFieldOnTwoFieldNoIndex(FormPrLs,cNalNac,TipNN,0,'FormPrLs')
        // Если этап завершен без ошибок для одной рез. МЦ
        if (_nMsgFrm = m_lErrOrJoi) SpJoi.Status := wOrd(3);
        else                        SpJoi.Status := wOrd(4);
        update current SpJoi;

        if (m_lErrOrJoi <> _nMsgFrm)
        {
          ErrObAll.PutEventById (feBreak, fcCycleOfErrOrAll);
          ErrObAll.write(_nMsgFrm - m_lErrOrJoi);
        }

        m_lErrOrJoi := _nMsgFrm;
    } while (GetNext SpJoi where ((    JoiHead.nRec == SpJoi.cJoiHead
                                   and (word(3)<>SpJoi.Status) )) = tsOk);
    m_bWriteInfoSpJoi := False;

    if (m_lErrOrJoi <> _nMsgFrm)
    {
      ErrObAll.PutEventById (feBreak, fcCycleOfObAll);
      ErrObAll.write(_nMsgFrm - m_lErrOrJoi);
    }
    // Если объединение без ошибок
    if (_nMsgFrm = m_lErrOrJoi) JoiHead.Status := wOrd(3); // статус "Завершено"
    else                       JoiHead.Status := wOrd(4);
    update Current JoiHead;      // статус "Завершено с ошибками"

    m_lErrOrJoi := _nMsgFrm;
    m_lErrOrJoi := m_lErrOrJoi;

  } While (GetNext JoiHead where ((m_wCgObedValue == JoiHead.TypeEvent And
                                   UserName() == JoiHead.UserName )) = tsOk And
                                   m_bBreak = False);

  if (_nMsgFrm > 0)
  {
    ErrObAll.PutEventById (feBreak, fcCycleOfResAll);
    ErrObAll.write(_nMsgFrm);
  }

  m_wParamOb := 0;
  DoneMarker (MyMarker,'');
  if (m_lcount_vis <> 0)
  {
    StopVisual('', 0);
    if (_nMsgFrm > 0)  // Если были ошибки во время объединения
    {
      message('Во время объединения произошли ошибки !'
              + chr(13) + 'Просмотрите протокол ошибок и если необходимо,'
              + chr(13) + 'запустите процедуру объединения налогов еще раз.' + chr(13));
      ErrObAll.ShowFile;
    }
  }
  Message('Объединение завершено.');
}

browse NalogJoi;
show at (,,,6);
Table JoiHead;
  recMarker = m_pMarkerUp {JoiHead.Nrec};
fields
  strOfficeJoiHead 'Офис', '───' ('',hcNastrAdmWOb,sci1378EscInsPMBrigadeDuty): [5], protect;
  sObName          'Наим. налога. (рез.объединения)'
                   ('', hcNastrAdmWOb, sci1378EscInsPMBrigadeDuty) : [32], Protect;
  strFilial        'филиал','───────'('',hcNastrAdmWOb,sci1Esc) : [7], Protect;
  strJoiStatus     'Статус'
                   ('', hcNastrAdmWOb, sci1Esc) : [14], Protect;
  JoiHead.UserName 'Пользователь'('', hcNastrAdmWOb, sci13Esc) : [12], Protect;
  strJoiCOrpo      '№ офиса','───────'('',hcNastrAdmWOb,sci1Esc) : [7], Protect;
end;

TableEvent Table JoiHead;
cmSetDefault :
{
  JoiHead.TypeEvent := m_wCgObedValue;
  JoiHead.Status := word(0);
  JoiHead.UserName := UserName();
  JoiHead.dDate := Cur_Date;
}

cmInsertRecord : insert current JoiHead;
cmUpdateRecord : update current JoiHead;

cmInsert :
{
  case curfield of
  #sObName :
  {
    if (RunInterface('GetNalog', m_cChoiceNrec)<>cmCancel)
    {
      if (not FindExistObj(m_cChoiceNrec)) putCommand(cmValue1);
      else abort;
    }
    else abort;
  }
  end;
}

cmMarkDelete :
{
  if (curtable = #JoiHead)
  {
    m_bDeleteByMarker := false;
    if (m_pMarkerUp.Count <> 0)
    {
      if (message('Удалить помеченные записи?', yesno + mfSwapButtons + confirmation) = cmYes)
      {
        m_bDeleteByMarker := true;
        stop;
      }
      else abort;
    }
  }
}

cmdeleteRecord :
{
  if not(isValid(tnJoiHead)) exit;
  if (not m_bDeleteByMarker) m_bExistDel := false; // инициализация переменной в случае когда пометки нет для случая обычного удаления

  //#5578 >>>>>>>>>>>>>>>>>
  var _boDiffUser : boolean = (JoiHead.UserName <> UserName());
  if _boDiffUser
    if (not m_bDeleteByMarker)
      if pr_CurUserAdmin
      { if message(lMsgOdebdifferenceUser
          +chr(13)+'Продолжить?' , YesNo)=cmYes
        { _boDiffUser:=false

        }
      }
  //#5578 <<<<<<<<<<<<<<<<<<<<<<<<<
  if _boDiffUser
  {
    if (not m_bDeleteByMarker)
      message(lMsgOdebdifferenceUser
        +chr(13)+'обратитесь к администратору БД', Warning);
    if (m_bExistDel and ((not m_bDeleteByMarker) or (m_bDeleteByMarker and m_pMarkerUp.Count = 0)))
      message('Удаление из каталога МЦ произведено.', Information + OkButton);
    exit;
  }

  var bRun : boolean; bRun := true;
  if (not m_bDeleteByMarker)
    bRun := message('Удалить запись?', yesno + mfSwapButtons + confirmation) = cmYes;

  if (bRun)
  {
    if (    (JoiHead.Status = word(3))
         Or (JoiHead.Status = word(4)))
    {
      if (getFirst SpJoi where ((    JoiHead.nRec == SpJoi.cJoiHead
                                 and byte(1)      == SpJoi.isDel
                                 and byte(4)      == SpJoi.Status )) = tsOk)
      {
        var bDelFromCatalog : boolean; bDelFromCatalog := false;
        if (not m_bDeleteByMarker)
          m_bDeleteByMarker := message('Удалять из каталога налогов, объединение'
                           + chr(13) + 'которых завершено с ошибками?',
                    yesno + mfSwapButtons + Confirmation) = cmYes;
        if (m_bDeleteByMarker)
          update SpJoi where ((    JoiHead.nRec == SpJoi.cJoiHead
                               and byte(1)      == SpJoi.isDel
                               and byte(4)      == SpJoi.Status)) set Status := 3;
      }

      _loop SpJoi where ((    JoiHead.nRec == SpJoi.cJoiHead
                          and byte(1)      == SpJoi.isDel
                          and byte(3)      == SpJoi.Status))
      {
        delete KatNalogTmp where ((SpJoi.cRec == KatnalogTmp.nrec));
        m_bExistDel := true;
      }
    }
    delete SpJoi where ((JoiHead.nRec == SpJoi.cJoiHead));
    delete current JoiHead;
    if (m_bExistDel and ((not m_bDeleteByMarker) or (m_bDeleteByMarker and m_pMarkerUp.Count = 0)))
      message('Операция удаления завершена.', Information + OkButton);
  } else Abort;
}
end;

hAndleevent
cmValue1 :
{
  set JoiHead.cRec := m_cChoiceNrec;
  UpdateTable;
  SetFilialInNastrObed(coJoiHead, KatNalog.Name, KatNalog.Atl_Branch);
  ReDrawPanel(#JoiHead);
}

cmPick :
{
  #Check_Pick(JoiHead)
  case curfield of
  //-----------
  #sObName :
  {
    if (JoiHead.Status <> 0) exit;
    m_cChoiceNrec := JoiHead.cRec;
    if (RunInterface('GetNalog', m_cChoiceNrec)<>cmCancel)
    {
      #CheckChoiceValueForJoiHeadOnPick
      UpDateTable;
      SetFilialInNastrObed(coJoiHead, KatNalog.Name, KatNalog.Atl_Branch);
      ReReadRecOrd(#JoiHead);
      ReDrawPanel(#JoiHead);
    }
  }
  end;
}
end; // hanldeevent

browse NalogSpJoi;
show at (,7,,17);
Table SpJoi;
fields
    strOfficeSpJoi  'Офис', '───' ('',hcNastrAdmWOb,sci1378Esc): [5], protect;
    sOb1Name        'Наим. налогов, подлежащих объединению'
                    ('', hcNastrAdmWOb, sci1378Esc): [41], Protect;
    strFilial1      'филиал','───────'('',hcNastrAdmWOb, sci1Esc) : [7], Protect;
    strSpJoiStatus  'Статус'
                    ('', hcNastrAdmWOb, sci1Esc)   : [15], Protect;
    cSpJoiDel       'Удалять'
                    ('Изменяется при нажатии на <F3>', hcNastrAdmWOb, sci13Esc)
                                                            : [7], #protectedcentered;
end;

tableEvent Table SpJoi;
cmInsert:
{
  if (JoiHead.UserName <> UserName())
  {
    message (lMsgOdebdifferenceUser, Warning);
    abort; exit;
  }

  if (JoiHead.Status <> 0) {abort; exit;}
  delete Pick where ((lPickConst == Pick.wList));
  if (RunInterface('GetSomeNalog') <> cmCancel)
  {
    _loop Pick where ((lPickConst == Pick.wList))
    {
      if (FindExistObj(Pick.cRec)) continue;
      ClearBuffer (#SpJoi);
      SpJoi.cJoiHead := JoiHead.nRec;
      SpJoi.cRec := Pick.cRec;
      SpJoi.Status := word(0);
      SpJoi.IsDel := word(1);
      Insert current SpJoi;
      SetFilialInNastrObed(coSpJoi, KatNalog1.Name, KatNalog1.Atl_Branch);
    }
    ReReadRecord(#SpJoi);
    ReDrawPanel(#SpJoi);
  }
  abort;
}

cmdeleteRecord :
{
  if (JoiHead.UserName <> UserName())
  {
    message (lMsgOdebdifferenceUser, Warning);
    exit;
  }

  if (JoiHead.Status = 0)
    if (message('Удалить запись?', yesno + mfSwapButtons + Confirmation) = cmYes)
      delete current SpJoi;
}
end;

hAndleevent
cmPick :
{
  #Check_Pick(SpJoi)

  case curfield of
  #sOb1Name :
  {
    if (JoiHead.Status <> 0) exit;
    m_cChoiceNrec := SpJoi.cRec;
    if (RunInterface('GetNalog', m_cChoiceNrec)<>cmCancel)
    {
      #CheckChoiceValueForSpJoiOnPick
      clearBuffer(#SpJoi);
      SpJoi.cJoiHead := JoiHead.nRec;
      SpJoi.cRec     := m_cChoiceNrec;
      SpJoi.Status   := word(0);
      SpJoi.IsDel    := word(1);
      insert current SpJoi;
      SetFilialInNastrObed(coSpJoi, KatNalog1.Name, KatNalog1.Atl_Branch);
      ReReadRecord(#SpJoi);
      ReDrawPanel(#SpJoi);
    }
  }
  #cSpJoiDel :
  {
    if (SpJoi.cRec <> comp(0))
    {
      if (SpJoi.IsDel = word(1)) SpJoi.IsDel := word(0);
      else                       SpJoi.IsDel := word(1);
      update current SpJoi;
      RedrawPanel (#SpJoi);
    }
  }
  end;
}

end; // hAndleevent

// Экран с кнопками
screen ButtonsList;
  show at (,18,,22);
  Fields
    m_wFilterByUser ('Изменяется при нажатии на <Пробел>', hcNastrAdmWOb, sci1Esc) : , noprotect;
    m_wCheckTimeObedRep ('Изменяется при нажатии на <Пробел>',hcNastrAdmWOb,sci1Esc): noprotect;
Buttons
  cmObed,Default,,,hcNastrAdmWOb,sci1Esc;
  cmCancel,,,,hcNastrAdmWOb,sci1Esc;

<<
  Показывать объединения
  (.) всех пользователей`      [.] Протокол по времени объединения`
  (.) только свои       `
                             <.~О~бъединить .>         <. О~т~мена .>
>>

end; // screen

hAndleevent
cmCheckField :
{
  case curfield of
  #m_wFilterByUser :
  {
    case m_wFilterByUser of
    0 :  if (BoundActive(tbOnlyUser))     PopBounds (tbOnlyUser);
    1 :  if (not BoundActive(tbOnlyUser)) PushBounds (tbOnlyUser);
    end;
    if (getFirst JoiHead = tsOk) {};

    ReReadRecord(#JoiHead);
    RescanPanel (#JoiHead);
    RescanPanel (#SpJoi);
    SelectPanel (#JoiHead);
    SaveMyDsk (m_wFilterByUser, lFilterByUserOb);
  }
  //#m_wNeedZarRun : SaveMyDsk (m_wNeedZarRun, lNeedZarRunOb);
  #m_wCheckTimeObedRep : SaveMyDsk (m_wCheckTimeObedRep, lCheckTimeObedRepOb);
  end; // case
}

cmInit:
{
  DisableCommand(cmFilterSave);
  m_wCodeTable := coKatNalog;
// функцию можно запускать при выборе режима одного филиала
  if (not CanMayRunObedInFilialDb)
  {
    CloseInterface(cmCancel);
    exit;
  }
  bDBPlatformType := GetDBPlatformType;// читаем тип СУБД
  delete Pick where ((lPickConst == Pick.wList));
  m_wCgObedValue := cgObedNalog;// присвоение должно быть раньше кода, где используется переменная m_wCgObedValue
  if (not ReadMyDsk (m_wNeedZarRun, lNeedZarRunOb, true)) m_wNeedZarRun := 3;
  if (not ReadMyDsk (m_wFilterByUser, lFilterByUserOb, true)) m_wFilterByUser := 0;
  if (not ReadMyDsk (m_wCheckTimeObedRep, lCheckTimeObedRepOb, true)) m_wCheckTimeObedRep := 0;
  if (m_wFilterByUser = 1) PushBounds (tbOnlyUser);

  m_wParamOb := 0;

  m_bCOrpo := InCorpo(m_wCgObedValue);

  if (m_bCOrpo)
  {
    DelOutCOrpo(m_wCgObedValue);
    CorpoForUser(m_wCgObedValue);
  }

  if (getFirst tmpJoiHead where ((    m_wCgObedValue == tmpJoiHead.TypeEvent
                                  and UserName()        == tmpJoiHead.UserName
                                  and (word(1)=tmpJoiHead.Status) )) = tsOk )
  {
    m_wParamOb := word(1);
    if (message('Обнаружено незавершенное объединение налогов'
                + chr(13) + 'рекомендуется немедленно продолжить это объединение.'
                + chr(13) + chr(13) + 'Запустить незавершенное объединение?',yesno + Confirmation) = cmYes)
      PutCommand(cmObed);
    else
      CloseInterface (cmCancel);
  }
}

cmDefault:
  AbOrt;

cmHotKeys :
if #IsBetaTester
  PutHotCommAnd(RunMenu('BetaObedNalog_HotKeys_Print'));
else
  PutHotCommAnd(RunMenu('ObedNalog_HotKeys_Print'));

cmPrintDoc :
{
  if (frmObedAll.errOr)
  {
    frmObedAll.abortform;
    message('Ошибка при формировании протокола соответствия.' + chr(13)
           +'Возможно, формы ObedAll нет в ресурсе.', CancelButton);
    exit;
  }
  else
    #PrintRelation(KatNalog1,KatNalog,Name,'налогов')
}

cmObed :
{
  _try
  {
    SetFilialBdToStandart;// обработка филиальности (перевод с филиальности в стандарт)
    mtClear (#ExecTime,mfNormal);
    if (m_wParamOb <> 1)
    {
      _loop tmpJoiHead where ((    m_wCgObedValue == tmpJoiHead.TypeEvent
                               and UserName()        == tmpJoiHead.UserName))
      {
        if (getFirst tmpSpJoi where ((tmpJoiHead.nRec == tmpSpJoi.cJoiHead)) <> tsOk)
        {
          var sObjName : string; sObjName := lNotDefine;
          If (GetFirst KatNalogTmp where (( tmpJoiHead.cRec == KatNalogTmp.nrec)) = tsOk )
            sObjName := trim(KatNalogTmp.Name);
          message ('Для результата объединения' + chr(13)
                 + '"' + sObjName + '"' + chr(13)
                 + 'не выбраны налоги, которые необходимо объединять.', CancelButton);
          SetStandartBdToFilial;// обработка филиальности (перевод со стандартной в филиальную)
          exit;
        }
      }
      if (getFirst tmpJoiHead where ((    m_wCgObedValue == tmpJoiHead.TypeEvent
                                      and UserName()        == tmpJoiHead.UserName
                                      and (word(4) = tmpJoiHead.Status) )) = tsOk )
        if (message('Хотите повторять объединение для' + chr(13)
                  + 'завершенных с ошибками налогов?', YesNo + Confirmation) = cmNo)
          m_wParamOb := 2;
    }
    if (IsNotExistNastroikiFromOtherFilials(m_wCgObedValue, m_wCodeTable)) updateObj;
   }
   _finally
   {
     SetStandartBdToFilial;// обработка филиальности (перевод со стандартной в филиальную)
   }
   ReReadRecOrd(#JoiHead);
   ReDrawPanel(#JoiHead);
   ReReadRecOrd(#SpJoi);
   ReDrawPanel(#SpJoi);
   If ((m_wCheckTimeObedRep = 1) and m_bShowRepTime)
     #MakeTimeProt('налогов');
}

cmValue2 :
{
  CheckProverka;
}

end;
end.  // Interface

ObedNalog_HotKeys_Print Menu
{
- 'Печать',cmPrintDoc,'Печать соответствия объединения',, 'Ctrl+P', kbCtrlP, scMenuCtx;
}

BetaObedNalog_HotKeys_Print Menu
{
- 'Печать',cmPrintDoc,'Печать соответствия объединения',, 'Ctrl+P', kbCtrlP, scMenuCtx;
- 'Проверка объединения ',cmValue2,'Проверка объединения',,'',, scMenuCtx;
}
